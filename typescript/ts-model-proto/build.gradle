plugins {
    id "java"
    id "com.google.protobuf"
    id "com.moowork.node"
}

def targetMainDir = "${project.buildDir}/generated/source/proto/main"

def nodeModulesPath = "${project.buildDir}/node"

node {
    workDir = gradle.nodeWorkingDir

    version = gradle.nodeVersion
    npmVersion = gradle.npmVersion
    yarnVersion = gradle.yarnVersion

    nodeModulesDir = file(nodeModulesPath)
    download = true
}

task addProtocGenTs(type: YarnTask) {
    outputs.dir(nodeModulesPath)
    args = ['add', 'ts-protoc-gen']
}



task moveGeneratedTs() {
    doLast {
        def tsDir = file("$targetMainDir/ts")

        if (tsDir.exists() && tsDir.isDirectory()) {
            tsDir.eachFile { file ->
                file.renameTo("$targetMainDir/js/${file.getName()}")
            }

            tsDir.delete()
        }
    }
}

dependencies {
    implementation("com.google.protobuf:protobuf-java:${gradle.protobufImplementationVersion}")
}

sourceSets {
    generated {
        java.srcDir("$targetMainDir/java")
    }

    main {
        proto {
            srcDirs = ['src']
        }

        java {
            srcDirs = ["$targetMainDir/java"]
        }
    }
}

protobuf {
    plugins {
        ts {
            path = file(nodeModulesPath).absolutePath + "/node_modules/ts-protoc-gen/bin/protoc-gen-ts"

            if (System.properties['os.name'].toLowerCase().contains('windows')) {
                path = path + ".cmd"
            }
        }
    }

    generateProtoTasks {
        all().each { task ->
            if(task.name == 'generateProto') {
                task.dependsOn(addProtocGenTs)
                task.finalizedBy(moveGeneratedTs)
            }

            task.builtins {
                js {
                    option "import_style=commonjs"
                    option "binary"
                }
            }

            task.plugins {
                ts { }
            }
        }
    }

    protoc {
        artifact = "com.google.protobuf:protoc:${gradle.protobufImplementationVersion}"
    }
}

