plugins {
  id "java"
  id "com.moowork.node" version "1.2.0"
}

task compileTs(type: NodeTask) {
  inputs.file("./tsconfig.json")
  inputs.dir("./src")
  outputs.dir("./build/ts")

  script = file("./node_modules/typescript/bin/tsc")

  args = ["-p", "tsconfig.json"]
}


task createJar(type: Zip) {
  dependsOn = [compileTs]
  baseName 'dukatts'
  extension 'jar'
  destinationDir file("${project.buildDir}/jar")

  from('build/ts') {
    into 'js'
  }

  from("${project.projectDir}/node_modules/typescript/lib/tsserverlibrary.js") {
    into 'ts'
  }

}

configurations {
  dukatTsResources
}

configurations.default.extendsFrom(configurations.dukatTsResources)


artifacts {
  dukatTsResources(createJar.archivePath) {
    builtBy createJar
    type "jar"
  }
}