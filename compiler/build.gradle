import de.undercouch.gradle.tasks.download.Download
import org.gradle.internal.os.OperatingSystem

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:3.4.3'
    }
}

plugins {
    id("kotlin")
}

configurations {
    toDownload
}

sourceSets {
    main.kotlin.srcDirs = ['src']
    //main.resources.srcDirs = ["${project.buildDir}/resources"]

    test.kotlin.srcDirs = ['test/src']
    test.resources.srcDirs = ['test/data']
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    if (OperatingSystem.current().isMacOsX()) {
        implementation 'com.eclipsesource.j2v8:j2v8_macosx_x86_64:4.6.0'
    } else if (OperatingSystem.current().isLinux()) {
        implementation 'com.eclipsesource.j2v8:j2v8_linux_x86_64:4.8.0'
    } else if (OperatingSystem.current().isWindows()) {
        implementation 'com.eclipsesource.j2v8:j2v8_win32_x86:4.6.0'
    } else {
        logger.error("unsupported platform - can not compile com.eclipsesource.j2v8 dependency")
    }

    implementation(project(":ast"))
    implementation(project(":ast-common"))
    implementation(project(":ast-model"))
    implementation(project(":tsmodel"))
    implementation(project(":interop-graal"))
    implementation(project(":tsinterop"))
    implementation(project(":itertools"))
    implementation(project(":node-lowering"))
    implementation(project(":node-introduction"))
    implementation(project(":ownerContext"))
    implementation(project(":translator"))
    implementation(project(":translator-string"))

    implementation(project(":j2v8"))

    implementation(project(":logging"))
    implementation(project(":panic"))

    runtime 'org.jetbrains.kotlin:kotlin-reflect'
    compile 'org.jetbrains.kotlin:kotlin-reflect'

    testImplementation 'org.jetbrains.kotlin:kotlin-reflect'

    testImplementation 'org.jetbrains.kotlin:kotlin-test-common'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    // without this dependency one won't see "Click to see difference" in IDEA
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'

    testImplementation "org.junit.jupiter:junit-jupiter-params:${gradle.jupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${gradle.jupiterVersion}"

    testImplementation "org.jetbrains.kotlin:kotlin-gradle-plugin"

    compile(project(path: ':ts', configuration: 'dukatTsResources'))

    toDownload 'org.jetbrains.kotlin:kotlin-stdlib-js'
}


processResources {
    from(zipTree(configurations.compile.find { it.name == "dukatts.jar" })) {
        into "/"
    }
}


task propagateToIdeaRuntime(type: Copy) {
    dependsOn = [":ts:createJar"]
    from(zipTree(configurations.compile.find { it.name == "dukatts.jar" }))
    into "out/production/resources/"
}

compileTestKotlin.dependsOn = ["propagateToIdeaRuntime"]

task download(type: Copy) {
    from configurations.toDownload
    into "${project.buildDir}/lib"
    rename "kotlin-stdlib-js-${gradle.kotlinVersion}.jar", "kotlin-stdlib-js.jar"
}


task downloadDefinitelyTyped(type: Download) {
    onlyIfModified true
    overwrite false
    def version = '10aca630788ef193f994ac31de63ea9cf44630a3'
    src "https://github.com/DefinitelyTyped/DefinitelyTyped/archive/${version}.zip"
    dest new File(rootProject.gradle.gradleUserHomeDir, "DefinitelyTyped-${version}.zip")
}

task fetchDefinitelyTyped(dependsOn: downloadDefinitelyTyped, type: Copy) {
    from zipTree(downloadDefinitelyTyped.dest)
    into "${rootProject.gradle.gradleUserHomeDir}/definitelyTyped"
}

test.dependsOn(download)

test {
    [
            "dukat.test.extended",
            "dukat.test.jsbackend.graal",
            "dukat.test.jsbackend.j2v8",
    ].forEach { String projectProperty ->
        if (project.hasProperty(projectProperty)) {
            systemProperty(projectProperty, "true")
        }
    }
    
    systemProperty("dukat.test.resources.definitelyTyped", "${rootProject.gradle.gradleUserHomeDir}/definitelyTyped")        
}